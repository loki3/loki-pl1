// Flexagons
//	Start from a flexagon configuration, e.g. `penta`, and apply a series of flexes
//	to rearrange the leaves, e.g. `penta > > S P`.
//	For background, see http://loki3.com/flex/index.html and http://loki3.com/flex/g4g10/Flex-Theory.pdf

:testFlex v= func0
	import :unittest.l3
	unittestPresent .[ :flexagon.l3 :flexagon_tests.l3

// sample flexagons
:hexagon v= .[ 1 2 3 4 5 6
:tri     v= .[ [2 1] 3 [5 4] 6 [8 7] 9
:penta   v= .[ [ [ [3 2] 4 ] 1 ] 5 [ [ [8 7] 9 ] 6 ] 10 [ [ [13 12] 14 ] 11 ] 15
// (((3,2)4)1) (5) (((8,7)9)6) (10) (((13,12)14)11) (15)


// routines for performing a general flex

:~ v= multifunc1
	when [ ->a ->b ]
		[ (~ b) (~ a) ]
	when ->leaf
		0 - leaf
:~ @cat :flex @doc ." Turn over a single pat

:defFlex v= func1 [ ->name ->alias ]
	{ :name name   :alias alias   :in ( l3.eval (body . 0) )   :out ( (body . 1) =>str ) }
:defFlex @body? true
:defFlex @cat :flex @doc ." Define a flex, providing its name, alias, input pattern, and output pattern

:makeFlex v= func1 [ ->name ->alias ->in ->out ]
	:temp v= 1multifunc
		when in
			l3.eval out
		when [ { :error ->error :flexes ->flexes :flexagon ->flexagon } ]
			.[ .{ :error "Can't apply flexes" :flexes ( flexes + alias ) :flexagon flexagon
		when (->flexagon : :array)
			.[ .{ :error "Can't apply flex" :flexes [alias] :flexagon flexagon
	:doc v= "Perform a " + name + " on a hexaflexagon"
	:temp @cat :flex @doc doc
	closure :temp
:makeFlex @cat :flex @doc ." Turn a flex definition into a prefix function with the given alias


// routines for generating the structure needed for a general flex

:flexgen v= .( ->flexagon 1func1 ->flex
	// e.g. [ [:a :b] :c ]  becomes  [ [1 2] 3 ]
	:number f= func1 [ [->pat0 ->pat1] ->getNext ]
		[ ( number [pat0 getNext] ) ( number [pat1 getNext] ) ]
	:number f= func1 [ ->n ->getNext ]
		// first use `actualN` from `expandPat`, after that use `getNext`
		:temp v= actualN
		(actualN =? 0) ? .r[ getNext (:actualN = 0  temp)

	// if pat doesn't have needed structure, create it
	:expandPat v= func1 .[ ->actualPat ->neededPat (->getNext : :function)
		match actualPat
			when neededPat
				actualPat
			when [->pat0 ->pat1]
				[ ( expandPat [ pat0 (neededPat . 0) getNext ] ) ( expandPat [ pat1 (neededPat . 1) getNext ] ) ]
			when ->actualN
				number [neededPat getNext]

	// find max in nested arrays
	:findMax f= func1 [->pat0 ->pat1]
		(findMax pat0) max (findMax pat1)
	:findMax f= (| ## |)

	// track the next number to use when creating leaves
	:next v= ( ( flexagon apply findMax ) fold max )
	:getNext v= (- :next ++ -) 

	flexagon apply (< expandPat [ #2 (flex . #1) getNext ] >)
:flexgen @cat :flex @doc ." If a flexagon doesn't have the necessary structure for a flex, add it
:flexgen @example ." [ [1 2] 3 4 5 6 7 ] flexgen [ [->a ->b] [ ->c [->d ->e] ] ->f ->g ->h [->i ->j] ]


// flex definitions

:def^ v= defFlex .[ :turn-over :^
	[ ->p1 ->p2 ->p3 ->p4 ->p5 ->p6 ]
	[ (~ p6) (~ p5) (~ p4) (~ p3) (~ p2) (~ p1) ]
:^ v= .( makeFlex def^

:def< v= defFlex .[ :move-left :<
	[ ->p1 ->p2 ->p3 ->p4 ->p5 ->p6 ]
	[ p6 p1 p2 p3 p4 p5 ]
:< v= .( makeFlex def<

:def> v= defFlex .[ :move-right :>
	[ ->p1 ->p2 ->p3 ->p4 ->p5 ->p6 ]
	[ p2 p3 p4 p5 p6 p1 ]
:> v= .( makeFlex def>

:defP v= defFlex .[ :pinch-flex :P
	[ [->p2 ->p1] ->p3 [->p5 ->p4] ->p6 [->p8 ->p7] ->p9 ]
	[ (~ p2) [ p4 (~ p3) ] (~ p5) [ p7 (~ p6) ] (~ p8) [ p1 (~ p9) ] ]
:P v= .( makeFlex defP

:defT v= defFlex .[ :tuck-flex :T
	[ [ [->p1 ->p2] ->p3 ] ->p4 ->p5 [->p6 ->p7] ->p8 ->p9 ]
	[ p2 p4 p5 [p6 p7] p8 [ (~ p1) [ p9 (~ p3) ] ] ]
:T v= .( makeFlex defT

:defT' v= defFlex .[ :inverse-tuck-flex :T'
	[ ->p1 ->p2 ->p3 [->p4 ->p5] ->p6 [->p7 [->p9 ->p8] ] ]
	[ [ [ (~ p7) p1] (~ p8 ) ] p2 p3 [p4 p5] p6 p9 ]
:T' v= .( makeFlex defT'

:defS v= defFlex .[ :pyramid-shuffle :S
	[ [->p1 ->p2] ->p3 ->p4 ->p5 [ [ [->p6 ->p7] ->p8] ->p9] ->p10 ]
	[ [p1 [p8 [p2 (~ p10) ] ] ] p3 p4 p5 [p7 p9] (~ p6) ]
:S v= .( makeFlex defS

:rotateFlexes v= [ def^ def< def> ]
:morphFlexes  v= [ defP defT defT' defS ]

:P' v= ( ->x 1func )
	x ^ P ^
:P' @cat :flex @doc ." Perform an inverse pinch flex on a hexaflexagon

:S' v= ( ->x 1func )
	x ^ > S ^ >
:S' @cat :flex @doc ." Perform an inverse pyramid shuffle on a hexaflexagon


// check for possible flexes

:findFlexes v= func1 .[ ( ->flexagon : :array ) ( ->flexes : :array )
	// List all the flexes possible for the given flexagon without rotating or flipping
	:findFlexesAtVertex v= func1 ->flexagon
		flexes filter (| ( (## . :in) v= flexagon ) !=? nil |) fold [ [ ] (< #1 + #2 . :alias >) ]
	// in: a flexagon, a flex to apply, a description of flexes already applied, and a list of flex sequences
	// out: new flexagon, a description of the flexes that have been applied so far, and the new sequences added on to the previous list of flex sequences
	:moveAndTry v= func1 .[ ->flexagon ->flex ->description ->sequences
		:newFlexagon v= .( flexagon .( l3.eval flex
		:possibilities v= findFlexesAtVertex newFlexagon
		:description = description + flex
		:newSequences v= ( possibilities =? nil ) ? r[ nil ( possibilities apply (| description + ## |) ) ]r
		{ :flexagon newFlexagon :description description :sequences (sequences + newSequences) }
	//
	// check for flexes at current vertex
	:prev v= { :flexagon flexagon :description "" :sequences (findFlexesAtVertex flexagon) }
	// rotate flexagon, searching for flexes at each vertex
	repeat 5 .` :prev = moveAndTry .[ (prev . :flexagon) :> (prev . :description) (prev . :sequences)
	// flip over and try again
	:prev = moveAndTry .[ flexagon :^ "" (prev . :sequences)
	repeat 5 .` :prev = moveAndTry .[ (prev . :flexagon) :> (prev . :description) (prev . :sequences)
	prev . :sequences
:findFlexes @cat :flex @doc ." Find all the flexes from a list that can be applied to the current flexagon

:allFlexes v= func1 ->flexagon
	findFlexes .[ flexagon morphFlexes
:allFlexes @cat :flex @doc ." Find all the flexes that can be applied to the current flexagon
