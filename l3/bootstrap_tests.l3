// unittests for bootstrap.l3

:(		@unittest func()
	14 assert=? ( 4 + 3 ) * 2
	10 assert=? 4 + ( 3 * 2 )

:[		@unittest func()
	:array assert=? l3.getType { :value [ 1 2 ] }
	4 assert=? l3.getCount [ 3 4 5 6 ]

:{		@unittest func()
	:map assert=? l3.getType { :value { :a 1 :b 2 } }
	2 assert=? l3.getCount { :a 1 :b 2 }

://		@unittest func()
	// assertTrue? false
	true

:`		@unittest func()
	8 assert=? l3.eval ` 5 + 3 `

:.(		@unittest func()
	10 assert=? 4 + .( 3 * 2

:.[		@unittest func()
	3 assert=? l3.getCount .[ 2 3 4

:.{		@unittest func()
	:map assert=? l3.getType .{ :value .{ :a 1 :b 2 

:.`		@unittest func()
	8 assert=? l3.eval .` 5 + 3

:/*		@unittest func()
	:a /* comment */ v= /* comment */ 5 + 3
	8 assert=? a

:"		@unittest func()
	:asdf assert=? " asdf "

:'		@unittest func()
	:asdf assert=? ' asdf '

:."		@unittest func()
	:asdf assert=? ." asdf

:.'		@unittest func()
	:asdf assert=? .' asdf

:v=		@unittest func()
	:a v= 5
	5 assert=? a

:=		@unittest func()
	:a v= 5
	:a = 4
	4 assert=? a

:var	@unittest func()
	var :a = 5
	5 assert=? a


//
// function definitions:

:func	@unittest func()
	:a v= func ->b
		b + 2
	7 assert=? func 5

:infix	@unittest func()
	:a v= .( ->b infix ->c
		b + c
	42 assert=? 40 a 2

:postfix @unittest func()
	:a v= .( ->b postfix
		b + 2
	5 assert=? 3 a

:func()	@unittest func()
	:a v= func()
		3.5
	3.5 assert=? a


//
// get & set

:.		@unittest func()
	4 assert=? { :a 3 :b 4 :c 5 } . :b
	3 assert=? [ 6 5 4 3 2 ] . 3
	// map default
	:a v= { :a 9 :b 12 } @@default 42
	12 assert=? a . :b
	42 assert=? a . :notThere
	// array default
	:b v= [ 200 400 ] @@default 314
	200 assert=? b . 0
	400 assert=? b . 1
	314 assert=? b . 2
	314 assert=? b . -1

:@		@unittest func()
	:a v= 1
	:a @ [ :blah :something ]
	:something assert=? ( l3.getMetadata { :key :a } ) . :blah

:.@		@unittest func()
	:a v= 1
	:a @ [ :blah :something ]
	:something assert=? :a .@ :blah

:.@@	@unittest func()
	:a v= 1
	:a @ [ :blah :something ]
	{ :blah :something } assert=? :a .@@

:@doc	@unittest func()
	:a v= 1
	:a @doc :whatever
	:whatever assert=? :a .@doc

:.@doc	@unittest func()
	:a v= 1.5
	:a @doc :whatever
	:whatever assert=? :a .@doc

:@cat	@unittest func()
	:a v= 1
	:a @cat :something
	:something assert=? :a .@cat

:.@cat	@unittest func()
	:a v= 1.5
	:a @cat :something
	:something assert=? :a .@cat

:@order @unittest func()
	:a v= (| ## + 2 |)
	// a should be applied after *
	:a @order 5
	12 assert=? a 5 * 2
	// a should be applied before *
	:a @order 1
	14 assert=? a 5 * 2

:@body? @unittest func()
	:a v= func()
		:result v= nil
		:line forEach body
			:result = result +a [ line ]
	:a @body? true
	:b v= a
		3
		4
	[ 3 4 ] assert=? b

:@@ @unittest func()
	:a v= 1
	a @@ [ :key 2 ]
	2 assert=? :a .@ :key

:@@type @unittest func()
	:a v= 1
	a @@type :int
	:int assert=? :a .@ :l3.param.type

:@@default @unittest func()
	:a v= 1
	a @@default 2
	2 assert=? :a .@ :l3.param.default

:@@rest @unittest func()
	:a v= 1
	a @@rest
	true assert=? :a .@ :l3.param.rest

:copy	@unittest func()
	:a v= 1
	1 assert=? copy a
	:str assert=? copy :str

:currentScope @unittest func()
	// create accumulator outside the nested scope
	:map v= { }
	if true
		// add some vars in this scope to examine
		:a v= 1
		:b v= 2
		var :i forEach currentScope
			// ignore vars that the forEach created
			if ( ( i . 0 ) =? :a |? ( i . 0 ) =? :b )
				:map = map +a { ( i . 0 ) ( i . 1 ) }
	{ :a 1 :b 2 } assert=? map

:setScopeName @unittest stubFunc


//
// more function definitions

:lambda	@unittest func()
	:2+ v= .( lambda .[ :x .` 2 + x
	3 assert=? 2+ 1

:((		@unittest func()
	:2+ v= (( :x ` 2 + x ` ))
	3 assert=? 2+ 1
	:mult v= (( [ :a :b ] ` a * b ` ))
	12 assert=? mult [ 6 2 ]

:.((	@unittest func()
	:2+ v= .(( :x ` 2 + x `
	3 assert=? 2+ 1

:lambda1	@unittest func()
	:2+ v= .( lambda1 .` ## + 2
	3 assert=? 2+ 1

:(|		@unittest func()
	:2+ v= (| ## + 2 |)
	3 assert=? 2+ 1

:.(|	@unittest func()
	:2+ v= .(| ## + 2
	3 assert=? 2+ 1

:lambda2	@unittest func()
	:div v= .( lambda2 ` #1 / #2 `
	3 assert=? 6 div 2

:(<		@unittest func()
	:div v= (< #1 / #2 >)
	3 assert=? 6 div 2

:.(<	@unittest func()
	:div v= .(< #1 / #2
	3 assert=? 6 div 2


//
// logic

:&? @unittest func()
	assertTrue?  true  &? true
	assertFalse? true  &? false
	assertFalse? false &? true
	assertFalse? false &? false

:|? @unittest func()
	assertTrue?  true  |? true
	assertTrue?  true  |? false
	assertTrue?  false |? true
	assertFalse? false |? false

:!? @unittest func()
	assertTrue?  !? false
	assertFalse? !? true

:=? @unittest func()
	assertTrue?  5 =? 5
	assertFalse? 5 =? 4

:!=? @unittest func()
	assertTrue?  5 !=? 4
	assertFalse? 5 !=? 5

:=any? @unittest func()
	assertTrue?  5 =any? [ 3 5 6 9 ]
	assertFalse? 4 =any? [ 3 5 6 9 ]


//
// arrays

:.. @unittest func()
	[ 3 4 5 6 ] assert=? ( 3 .. 6 )

:+a @unittest func()
	[ 3 4 5 6 ] assert=? [ 3 4 ] +a [ 5 6 ]
	[ 3 4 5 6 ] assert=? [ 3 4 5 ] +a 6
	[ 3 4 5 6 ] assert=? [ 3 4 5 6 ] +a [ ]
	[ 3 4 5 6 ] assert=? [ 3 4 5 6 ] +a nil

:+= @unittest func()
	// array addition
	:a v= [ 3 4 5 ]
	// todo: perhaps it should be :a += 6
	a += 6
	[ 3 4 5 6 ] assert=? a

:countOf @unittest func()
	3 assert=? countOf .[ 5 6 8
	2 assert=? countOf .{ :a 1 :b 3
	5 assert=? countOf ." qwert

:apply @unittest func()
	[ 6 10 14 ] assert=? [ 3 5 7 ] apply (| ## * 2 |)

:foldLeft @unittest func()
	1 assert=? [ 27 9 3 ] foldLeft (< #1 / #2 >)

:foldRight @unittest func()
	9 assert=? [ 27 9 3 ] foldRight (< #1 / #2 >)

:filter @unittest func()
	[ 3 5 7 ] assert=? [ 2 3 4 5 6 7 8 ] filter (| ## % 2 =? 1 |)

:makeArray @unittest func()
	:a v= makeArray
		:asdf
		3.5
		2
	[ :asdf 3.5 2 ] assert=? a


//
// maps

:makeMap @unittest func()
	:a v= makeMap
		:1 2
		:3 4
	{ :1 2 :3 4 } assert=? a


//
// conditional

:? @unittest func()
	2 assert=? true  ? [ 2 3 ]
	3 assert=? false ? [ 2 3 ]

:if @unittest func()
	// if is run
	:a v= false
	if true
		:a = true
	assertTrue? a
	// if is skipped
	if false
		assertFail ." if shouldn't have been called

:elsif @unittest func()
	// if is run, not elsif
	:a v= 0
	if true
		:a = 1
	elsif true
		assertFail ." elsif shouldn't have been called
	1 assert=? a
	// elsif is run
	:a v= 0
	if false
		assertFail ." if shouldn't have been called
	elsif true
		:a = 2
	2 assert=? a
	// if & elsif are skipped
	:a v= 0
	if false
		assertFail ." if shouldn't have been called
	elsif false
		assertFail ." elsif shouldn't have been called
	0 assert=? a

:else @unittest func()
	// if is run, not else
	:a v= 0
	if true
		:a = 1
	else
		assertFail ." else shouldn't have been called
	1 assert=? a
	// else is run
	:a v= 0
	if false
		assertFail ." if shouldn't have been called
	else
		:a = 2
	2 assert=? a
	
	// if is run
	:a v= 0
	if true
		:a = 1
	elsif true
		assertFail ." elsif shouldn't have been called
	else
		assertFail ." else shouldn't have been called
	1 assert=? a
	// elsif is run
	:a v= 0
	if false
		assertFail ." if shouldn't have been called
	elsif true
		:a = 2
	else
		:a = 3
	2 assert=? a
	// else is run
	:a v= 0
	if false
		assertFail ." if shouldn't have been called
	elsif false
		assertFail ." elsif shouldn't have been called
	else
		:a = 3
	3 assert=? a


//
// loop

:while @unittest func()
	:total v= 0
	:i v= 0
	while .` i !=? 5
		:i = i + 1
		:total = total + i
	5  assert=? i
	15 assert=? total

:for @unittest func()
	// todo: should be able to declare i inside for
	:total v= 0
	:i v= 0
	for .[ ` :i = 0 ` ` i !=? 5 ` ` :i = i + 1 `
		:total = total + i
	10 assert=? total

:forEach @unittest func()
	:total v= 0
	:i forEach ( 1 .. 5 )
		:total = total + i
	15 assert=? total
	:total v= 0
	:i forEach { :a 2 :b 4 }
		:total = total + i . 1
	6 assert=? total

:forEachDelim @unittest func()
	// create a function that turns a body into a map
	:thing v= func()
		:result v= nil
		:map forEachDelim [ body :{ ]
			:result = result +a map
	:thing @body? true
	// use that function to create a map
	:test v= thing
		:a 5
		:b 7
	{ :a 5 :b 7 } assert=? test

:break @unittest func()
	:total v= 0
	:i v= 0
	while .` i !=? 8
		:i = i + 1
		if i =? 4
			break
		:total = total + i
	6 assert=? total


//
// strings

:=>str @unittest func()
	:5				assert=? 5 =>str
	:5.5			assert=? 5.5 =>str
	:5				assert=? :5 =>str
	" [ 1 2 3 ] "	assert=? [ 1 2 3 ] =>str

:+s @unittest func()
	:qwert assert=? :qw +s :ert

:"" @unittest func()
	:string	assert=? l3.getType .{ :value ""
	0		assert=? l3.getCount ""

:""" @unittest func()
	:a v= """
		this is
		some multiline
		text
	" this is some multiline text " assert=? a

:\n @unittest stubFunc


//
// enumerated type

:enum @unittest func()
	:asdf enum [ :qwert :yuiop ]
	0		assert=? asdf.qwert
	1		assert=? asdf.yuiop
	:asdf	assert=? l3.getType .{ :value asdf.yuiop


//
// math

:+ @unittest func()
	42 assert=? 39 + 3

:- @unittest func()
	7 assert=? 10 - 3

:* @unittest func()
	10 assert=? 5 * 2

:/ @unittest func()
	3 assert=? 9 / 3

:% @unittest func()
	3 assert=? 15 % 4

:sqrt @unittest func()
	assertTrue? ( ( sqrt 4 ) - 2 ) <? 0.00000001
	assertTrue? ( ( sqrt 2 ) - 1.414213562 ) <? 0.00000001

:<? @unittest func()
	assertTrue?  4 <? 5
	assertFalse? 5 <? 4

:>? @unittest func()
	assertTrue?  5 >? 4
	assertFalse? 4 >? 5

:min @unittest func()
	4 assert=? 4 min 7

:max @unittest func()
	7 assert=? 4 max 7


//
// modules

:import @unittest func()
	false


//
// I/O

:print @unittest func()
	false
